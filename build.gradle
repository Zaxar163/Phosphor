buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'http://files.minecraftforge.net/maven' }
    }

    dependencies {
        classpath 'ru.zaxar163.fixed.net.minecraftforge.gradle:ForgeGradle:2.3.1-SNAPSHOT'
        classpath 'ru.zaxar163.fixed.org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

group = "ru.zaxar163"

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft_version}-${mod_version}+build${System.getenv("BUILD_NUMBER")}"
} else {
    version = "${minecraft_version}-${mod_version}-SNAPSHOT"
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = forge_mappings

    clientJvmArgs += "-Dfml.coreMods.load=ru.zaxar163.phosphor.core.PhosphorFMLLoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=ru.zaxar163.mods.phosphor.core.PhosphorFMLLoadingPlugin"

    makeObfSourceJar = false
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

def coreModManifest = {
    attributes "FMLAT": "phosphor_at.cfg"
    attributes "Maven-Artifact": "${group}:${name}:${version}"
    attributes "FMLCorePlugin": "ru.zaxar163.phosphor.core.PhosphorFMLLoadingPlugin"
    attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
    attributes "MixinConfigs": "mixins.phosphor.json"
    attributes "TweakOrder": 0
    attributes "FMLCorePluginContainsFMLMod": true
    attributes "ForceLoadAsMod": true
}

jar {
    archiveClassifier = 'universal'

    manifest coreModManifest

    from sourceSets.main.output

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'

            exclude '**/*.html'
        }
    }


    rename '(.+_at.cfg)', 'META-INF/$1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'

    manifest coreModManifest

    from sourceSets.main.allSource
}

task deobfJar(type: Jar, dependsOn: reobfJar) {
    archiveClassifier = 'dev'

    manifest coreModManifest

    from sourceSets.main.output
}

import net.minecraftforge.gradle.user.TaskExtractAnnotations

task deobfJarExtractAnnotations(type: TaskExtractAnnotations, dependsOn: deobfJar) {
    jar = deobfJar.archivePath
}

deobfJar.finalizedBy deobfJarExtractAnnotations

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    defaultObfuscationEnv searge
    //add sourceSets.main, "mixins.phosphor.refmap.json"
}
sourceSets {
    main {
        ext.refMap = "mixins.phosphor.refmap.json"
    }
}

tasks.withType(JavaCompile) {
   options.encoding = "UTF-8"
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

build.dependsOn deobfJar, sourcesJar